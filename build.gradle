plugins {
    id 'java'
    id 'maven-publish'
}

group 'com.koralix.stepfn'
version '1.1.1'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

java {
    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = "com.koralix.stepfn:step-functions"
                description = 'Step Functions is a java library to create functions step-by-step with multi-branch logic.'
                url = 'https://github.com/Koralix-Studios/step-functions'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://github.com/Koralix-Studios/step-functions/blob/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        name = 'JohanVonElectrum'
                        email = 'johanvonelectrum@gmail.com'
                        organization = 'Koralix Studios'
                        organizationUrl = 'https://www.koralix.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/koralix-studios/step-functions.git'
                    developerConnection = 'scm:git:ssh://github.com/koralix-studios/step-functions.git'
                    url = 'https://github.com/Koralix-Studios/step-functions/tree/master'
                }
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/koralix-studios/step-functions")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

// add the overview.html file to the javadoc
javadoc {
    title = "Step Functions - ${version}"
    options {
        overview = file("${rootDir}/javadoc/overview.html").absolutePath
        windowTitle = "step-functions:${project.version}"
        header = file("${rootDir}/javadoc/header.html").text.replaceAll("@version@", project.version as String)
        locale = "en"
    }
}